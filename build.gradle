buildscript {
    configurations.classpath {
        resolutionStrategy {
            //this jgit version is required to fix this issue:
            //https://github.com/diffplug/spotless/issues/587 with the spotless plugin
            force 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
        }
    }
}

plugins {
    id "java-library"
    id "java-test-fixtures"
    id "jacoco"
    id "maven-publish"
    id "signing"
    id "application"
    id "io.codearte.nexus-staging" version "0.30.0"
    id "com.diffplug.spotless" version "5.11.1"
    id "com.github.ben-manes.versions" version "0.38.0"
    id "pl.allegro.tech.build.axion-release" version "1.13.1"
    id "org.sonarqube" version "3.1.1"
    id "nebula.lint" version "16.17.1"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "com.bmuschko.docker-remote-api" version "6.7.0"
    id "com.avast.gradle.docker-compose" version "0.14.2"
    id "com.github.michaelruocco.gradle-postman-runner" version "0.1.3"
}

group "com.github.michaelruocco"

ext {
    slf4jVersion = "1.7.30"
    lombokVersion = "1.18.20"
    jsonAdapterVersion = "1.0.0"
    jwtVersion = "9.7"
    jacksonVersion = "2.12.2"

    junitVersion = "5.7.1"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "${name}-"
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
}

gradleLint {
    rules = ['all-dependency']
    alwaysRun = false
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    runtimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "com.nimbusds:nimbus-jose-jwt:9.7"
    implementation "com.github.tomakehurst:wiremock-jre8:2.27.2"
    implementation "com.github.michaelruocco:file-loader:6.0.1"
    implementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    implementation "com.github.michaelruocco:json-adapter:${jsonAdapterVersion}"
    implementation "net.minidev:json-smart:2.3"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.beust:jcommander:1.81"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.19.0"
    testImplementation "com.nimbusds:nimbus-jose-jwt:${jwtVersion}"
    testImplementation "uk.org.webcompere:system-stubs-core:1.2.0"

    testFixturesCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testFixturesAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testFixturesImplementation "com.github.michaelruocco:rest-client:0.1.8"
    testFixturesImplementation "com.github.michaelruocco:json-adapter:${jsonAdapterVersion}"
    testFixturesImplementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    testFixturesImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    testFixturesImplementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    testFixturesImplementation "com.nimbusds:nimbus-jose-jwt:${jwtVersion}"
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams true
    }
}

mainClassName = "uk.co.mruoc.fake.jwt.Main"

shadowJar {
    mergeServiceFiles()
}

application {
    mainClass = mainClassName
}

docker {
    registryCredentials {
        username.set(System.getenv("DOCKER_USERNAME"))
        password.set(System.getenv("DOCKER_PASSWORD"))
    }
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage

task buildImage(type: DockerBuildImage) {
    inputDir.set(file("."))
    images.add("michaelruocco/${project.name}:${version}")
    buildArgs = ["VERSION": version]
}

dockerCompose {
    environment.put 'APP_VERSION', version
}

postman {
    collections = fileTree(dir: "postman", include: "*_collection.json")
    stopOnError = true
}

task tagImage(type: DockerTagImage) {
    dependsOn buildImage
    imageId = buildImage.getImageId()
    tag = "latest"
    repository = "michaelruocco/fake-jwt-auth-server"
}

task pushImage(type: DockerPushImage) {
    images.set(buildImage.images)
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    dependsOn test
    sourceSets sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

project.tasks["sonarqube"].dependsOn jacocoTestReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_${name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/report.xml"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def gitUrl = "https://github.com/michaelruocco/fake-jwt-auth-server"
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "mruoc"
            name "Michael Ruocco"
            email "michael.ruocco@hotmail.com"
        }
    }

    scm {
        url gitUrl
    }
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = toRepoUrl(project.version)
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        lib(MavenPublication) {
            from components.java
            suppressPomMetadataWarningsFor("testFixturesApiElements")
            suppressPomMetadataWarningsFor("testFixturesRuntimeElements")
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Template repo to speed up creating new library projects")
                root.appendNode("name", project.name)
                root.appendNode("url", gitUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

signing {
    def signingKey = System.getenv("OSSRH_PGP_SECRET_KEY")
    def signingPassword = System.getenv("OSSRH_PGP_SECRET_KEY_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.lib
}
signLibPublication.enabled = gradle.startParameter.taskNames.contains("publish")

def toRepoUrl(version) {
    if (version.endsWith('SNAPSHOT')) {
        return "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    return "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

nexusStaging {
    username = System.getenv("OSSRH_USERNAME")
    password = System.getenv("OSSRH_PASSWORD")
}

spotless {
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        replace 'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace 'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace 'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}
